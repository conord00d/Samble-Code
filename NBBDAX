I've left out quite a few measures that are involved to achieve the final measure of "Days On Hand Projection Switch" and attempted to keep this code focused on sharing various functions I've utilized.

----------------------------------------------------------------------------------------

144oz EQ Inventory =
CALCULATE( 
    SUMX( 
        f_Inventory, 
        [Quantity] * 
        RELATED( 'd_Item'[Ounces] )
    ),
    f_Inventory[UnitOfMeasure] = "C",         -- Distributors either report in bottles "B" or cases "C"
    f_Inventory[TransCode] = 10,              -- Code for current inventory qty
    d_Date[Dateid] = MAX( f_Inventory[PostingDate] )
) * (1/144)                                   -- Calculates the equivalent to a 144oz case
+
CALCULATE( 
    SUMX( 
        f_Inventory, 
        [Quantity] * 
        RELATED( 'd_Item'[Ounces] )
    ),
    f_Inventory[UnitOfMeasure] = "B",          -- Distributors either report in bottles "B" or cases "C"
    f_Inventory[TransCode] = 10,               -- Code for current inventory qty
    d_Date[DateId] = MAX( f_Inventory[PostingDate] )
) * (1/144) * (1/12) 

------------------------------------------------------------------------------------

Depletions Last 4 Weeks = 
VAR vDates =                -- Establish list of dates that meet business day criteria
FILTER(
    ALL( d_Date ),
    d_Date[IsPast] = 1 &&
    d_Date[IsWeekend] = 0 &&
    d_Date[IsHoliday] = 0
)

VAR vFilteredDates =        -- Filter list to only show last 20 of them (Last 20 business days)
TOPN( 20, vDates, [Date] )        

RETURN

CALCULATE(
    [Depletions 144oz EQ],
    vFilteredDates            -- Calculate depletions (in ounces) for last 20 business days
)

------------------------------------------------------------------------------------

Depletions Daily Switch = 
SWITCH(
    VALUES( p_DepletionParameter[Order] ),                -- Refers to a disconnected table whose fields are dropped onto a slicer that users can toggle the output
    1, DIVIDE( [Depletions Last 4 Weeks], 20 ),
    2, DIVIDE( [Depletions Last 4 Weeks +10%], 20 ),
    3, DIVIDE( [Depletions Last 4 Weeks +20%], 20 ),
    4, DIVIDE( [Depletions Last 4 Weeks +30%], 20 ),
    5, DIVIDE( [Depletions Last 6 Weeks], 30 ),
    6, DIVIDE( [Depletions Last 6 Weeks +10%], 30 ),
    7, DIVIDE( [Depletions Last 6 Weeks +20%], 30 ),
    8, DIVIDE( [Depletions Last 6 Weeks +30%], 30 ),
    9, DIVIDE( [Depletions Last 8 Weeks], 40 ),
    10,DIVIDE( [Depletions Last 8 Weeks +10%], 40 ),
    11,DIVIDE( [Depletions Last 8 Weeks +20%], 40 ),
    12,DIVIDE( [Depletions Last 8 Weeks +30%], 40 )
)

------------------------------------------------------------------------------------

Days On Hand Projection Switch = 
DIVIDE(
    [144oz EQ Inventory Latest],
    [Depletions Daily Switch]
)
